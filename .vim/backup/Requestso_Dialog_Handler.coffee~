FormKo = App.loadClass "portal::viewmodels/requestso/Form"
ValidationEngine = App.loadClass "core::ValidationEngine"

class exports.Requestso_Dialog_Handler extends FullPageHandler

    init:()->
        @layoutUrl = 'requestso/load_layout_requestso_dialog'
        @saveUrl = 'requestso/save_stock_order_request'

        @subHandlers =
            'ConfirmRequestDialog': # this is needed!
                'class': 'portal::handlers/requestso/Requestso_Confirm_Request_Dialog_Handler'

        @destroyOnSave = true

        @validator = new ValidationEngine()

        @createDialog()
        @openDialog()


    afterLoadLayout: (selector, response)->
        if response.status?.type isnt 'success'
            return

        ths = this
        @dialog.find('.modal-body').resizr()

        @koForm = new FormKo @, response.data
        ko.applyBindings @koForm,@dialog[0]


        @validator.makeFormValidator 'stock_order', "##{@dialog.attr('id')}", "#requestso-errors", (valid)->
            if valid
                handler = ths.createSubHandler('ConfirmRequestDialog')
                if handler
                    handler.init()

        @validator.registerRule
            selector: '#btn-add-line'
            text: 'You must have at least one line on the stock order request'
            func: (elem)=>
                return @koForm.stockOrder.lines().length > 0

        @dialog.find('.save-btn').bind 'click', ()=>
            @validator.validateAll()

        tbl_size = @dialog.find('.modal-body').height() - 140
        @table = @dialog.find('.table').dataTable
            sScrollY: tbl_size+'px'
            sScrollX: "100%"
            bScrollCollapse: true
            bPaginate: false
            bFilter: false
            bInfo: false
            bSort: false
            bEmpty: false

        @koForm.afterApplyBindings()
        @table.fnAdjustColumnSizing(false)

        $(window).unbind('modal_resized').bind 'modal_resized',debounce ()=>
            tbl_size = @dialog.find('.modal-body').height() - 140
            @table.fnSettings().sY = tbl_size+'px'
            @dialog.find('.dataTables_scrollBody').height(tbl_size)
            @table.fnAdjustColumnSizing(false)
